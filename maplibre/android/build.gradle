group = "com.github.josxha.maplibre"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "2.1.20"
    repositories {
        google()
        mavenCentral()
        maven { // repository used only for ktlint
            url = uri("https://plugins.gradle.org/m2/")
        }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.12.3")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        // noinspection GradleDynamicVersion
        classpath("org.jlleitschuh.gradle:ktlint-gradle:14.0.+")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    // Keeping the classes from being removed by proguard.
    defaultConfig {
        consumerProguardFiles 'consumer-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    if (project.android.hasProperty("namespace")) {
        namespace = "com.github.josxha.maplibre"
    }

    compileSdk = 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 21
    }

    dependencies {
        // jnigen picks up api dependencies
        api 'org.maplibre.gl:android-sdk:12.3.+'
        testImplementation "org.jetbrains.kotlin:kotlin-test"
        testImplementation "org.mockito:mockito-core:5.21.+"
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

ktlint {
    version = "1.7.1"
    android = true
    ignoreFailures = false
    reporters {
        reporter("plain")
        reporter("checkstyle")
    }
    dependencies {
        // noinspection GradleDynamicVersion
        ktlintRuleset("io.nlopez.compose.rules:ktlint:0.5.3")
    }
    filter {
        exclude("**/*.g.kt")
    }
}